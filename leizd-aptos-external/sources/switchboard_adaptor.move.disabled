// https://switchboard.xyz/
// https://docs.switchboard.xyz/aptos/feeds
// https://github.com/switchboard-xyz/sbv2-aptos
module leizd_aptos_external::switchboard_adaptor {
    use std::error;
    use std::signer;
    use std::string::String;
    use aptos_std::event;
    use aptos_std::simple_map;
    use aptos_framework::account;
    use leizd_aptos_common::permission;
    use leizd_aptos_common::coin_key::{key};
    use switchboard::aggregator;
    use switchboard::math;

    const ENOT_INITIALIZED: u64 = 1;
    const EALREADY_INITIALIZED: u64 = 2;
    const ENOT_REGISTERED: u64 = 3;
    const EALREADY_REGISTERED: u64 = 4;

    struct Storage has key {
        aggregators: simple_map::SimpleMap<String, address>
    }

    struct UpdateAggregatorEvent has store, drop {
        key: String,
        aggregator: address,
    }
    struct SwitchboardAdaptorEventHandle has key {
        update_aggregator_event: event::EventHandle<UpdateAggregatorEvent>,
    }

    ////////////////////////////////////////////////////
    /// Manage module
    ////////////////////////////////////////////////////
    public entry fun initialize(owner: &signer) {
        let owner_addr = signer::address_of(owner);
        permission::assert_owner(owner_addr);
        assert!(!exists<Storage>(owner_addr), error::invalid_argument(EALREADY_INITIALIZED));
        move_to(owner, Storage { aggregators: simple_map::create<String, address>() });
        move_to(owner, SwitchboardAdaptorEventHandle {
            update_aggregator_event: account::new_event_handle<UpdateAggregatorEvent>(owner),
        });
    }

    public entry fun add_aggregator<C>(owner: &signer, aggregator: address) acquires Storage, SwitchboardAdaptorEventHandle {
        let owner_addr = signer::address_of(owner);
        permission::assert_owner(owner_addr);
        let key = key<C>();
        assert!(exists<Storage>(owner_addr), error::invalid_argument(ENOT_INITIALIZED));
        assert!(!is_registered(key), error::invalid_argument(EALREADY_REGISTERED));
        let aggrs = &mut borrow_global_mut<Storage>(owner_addr).aggregators;
        simple_map::add<String, address>(aggrs, key, aggregator);
        event::emit_event<UpdateAggregatorEvent>(
            &mut borrow_global_mut<SwitchboardAdaptorEventHandle>(owner_addr).update_aggregator_event,
            UpdateAggregatorEvent { key, aggregator },
        );
    }
    fun is_registered(key: String): bool acquires Storage {
        let storage_ref = borrow_global<Storage>(permission::owner_address());
        is_registered_internal(key, storage_ref)
    }
    fun is_registered_internal(key: String, storage: &Storage): bool {
        simple_map::contains_key(&storage.aggregators, &key)
    }

    ////////////////////////////////////////////////////
    /// Feed
    ////////////////////////////////////////////////////
    fun price_from_aggregator(aggregator_addr: address): (u128, u8) {
        let latest_value = aggregator::latest_value(aggregator_addr);
        let (value, dec, _) = math::unpack(latest_value);
        (value, dec) // TODO: use neg in struct SwitchboardDecimal
    }
    fun price_internal(key: String): (u128, u8) acquires Storage {
        let owner_addr = permission::owner_address();
        assert!(exists<Storage>(owner_addr), error::invalid_argument(ENOT_INITIALIZED));
        assert!(is_registered(key), error::invalid_argument(ENOT_REGISTERED));
        let aggrs = &borrow_global<Storage>(owner_addr).aggregators;
        let aggregator_addr = simple_map::borrow<String, address>(aggrs, &key);
        price_from_aggregator(*aggregator_addr)
    }
    public fun price<C>(): (u128, u8) acquires Storage {
        let (value, dec) = price_internal(key<C>());
        (value, dec)
    }
    public fun price_of(name: &String): (u128, u8) acquires Storage {
        let (value, dec) = price_internal(*name);
        (value, dec)
    }

    #[test_only]
    use std::vector;
    #[test_only]
    use std::unit_test;
    #[test_only]
    use aptos_framework::block;
    #[test_only]
    use aptos_framework::timestamp;
    #[test_only]
    use leizd_aptos_lib::math128;
    #[test_only]
    use leizd_aptos_common::test_coin::{WETH, USDC};
    #[test(owner = @leizd_aptos_external)]
    fun test_initialize(owner: &signer) {
        let owner_addr = signer::address_of(owner);
        account::create_account_for_test(owner_addr);
        initialize(owner);
        assert!(exists<Storage>(owner_addr), 0);
        assert!(exists<SwitchboardAdaptorEventHandle>(owner_addr), 0);
    }
    #[test(account = @0x111)]
    #[expected_failure(abort_code = 65537)]
    fun test_initialize_with_not_owner(account: &signer) {
        initialize(account);
    }
    #[test(owner = @leizd_aptos_external)]
    #[expected_failure(abort_code = 65538)]
    fun test_initialize_twice(owner: &signer) {
        account::create_account_for_test(signer::address_of(owner));
        initialize(owner);
        initialize(owner);
    }
    #[test(owner = @leizd_aptos_external)]
    fun test_add_aggregator(owner: &signer) acquires Storage, SwitchboardAdaptorEventHandle {
        let owner_addr = signer::address_of(owner);
        account::create_account_for_test(owner_addr);
        initialize(owner);
        add_aggregator<WETH>(owner, @0xAAA);
        let aggregator = simple_map::borrow(&borrow_global<Storage>(owner_addr).aggregators, &key<WETH>());
        assert!(aggregator == &@0xAAA, 0);
        assert!(event::counter<UpdateAggregatorEvent>(&borrow_global<SwitchboardAdaptorEventHandle>(owner_addr).update_aggregator_event) == 1, 0);
    }
    #[test(account = @0x111)]
    #[expected_failure(abort_code = 65537)]
    fun test_add_aggregator_with_not_owner(account: &signer) acquires Storage, SwitchboardAdaptorEventHandle {
        add_aggregator<WETH>(account, @0xAAA);
    }
    #[test(owner = @leizd_aptos_external)]
    #[expected_failure(abort_code = 65537)]
    fun test_add_aggregator_before_initialize(owner: &signer) acquires Storage, SwitchboardAdaptorEventHandle {
        add_aggregator<WETH>(owner, @0xAAA);
    }
    #[test(owner = @leizd_aptos_external)]
    #[expected_failure(abort_code = 65540)]
    fun test_add_aggregator_twice(owner: &signer) acquires Storage, SwitchboardAdaptorEventHandle {
        let owner_addr = signer::address_of(owner);
        account::create_account_for_test(owner_addr);
        initialize(owner);
        add_aggregator<WETH>(owner, @0xAAA);
        add_aggregator<WETH>(owner, @0xAAA);
    }
    #[test(owner = @leizd_aptos_external, usdc_aggr = @0x111AAA, weth_aggr = @0x222AAA, aptos_framework = @aptos_framework)]
    fun test_end_to_end(owner: &signer, usdc_aggr: &signer, weth_aggr: &signer, aptos_framework: &signer) acquires Storage, SwitchboardAdaptorEventHandle {
        account::create_account_for_test(signer::address_of(aptos_framework));
        block::initialize_for_test(aptos_framework, 1);
        timestamp::set_time_has_started_for_testing(aptos_framework);
        aggregator::new_test(usdc_aggr, 2, 0, false);
        aggregator::new_test(weth_aggr, 3, 0, false);

        account::create_account_for_test(signer::address_of(owner));
        initialize(owner);
        add_aggregator<USDC>(owner, signer::address_of(usdc_aggr));
        add_aggregator<WETH>(owner, signer::address_of(weth_aggr));

        let (usdc_value, usdc_dec) = price_from_aggregator(signer::address_of(usdc_aggr));
        assert!(usdc_value == 2 * math128::pow(10, 9), 0);
        assert!(usdc_dec == 9, 0);
        let (weth_value, weth_dec) = price_from_aggregator(signer::address_of(weth_aggr));
        assert!(weth_value == 3 * math128::pow(10, 9), 0);
        assert!(weth_dec == 9, 0);
        let (usdc_value, usdc_dec) = price<USDC>();
        assert!(usdc_value / math128::pow(10, (usdc_dec as u128)) == 2, 0);
        let (weth_value, weth_dec) = price<WETH>();
        assert!(weth_value / math128::pow(10, (weth_dec as u128)) == 3, 0);
        let (usdc_value, usdc_dec) = price_of(&key<USDC>());
        assert!(usdc_value / math128::pow(10, (usdc_dec as u128)) == 2, 0);
        let (weth_value, weth_dec) = price_of(&key<WETH>());
        assert!(weth_value / math128::pow(10, (weth_dec as u128)) == 3, 0);
    }
    #[test(aptos_framework = @aptos_framework)]
    fun test_aggregator(aptos_framework: &signer) {
        account::create_account_for_test(signer::address_of(aptos_framework));
        block::initialize_for_test(aptos_framework, 1);
        timestamp::set_time_has_started_for_testing(aptos_framework);

        let signers = unit_test::create_signers_for_testing(1);
        let acc1 = vector::borrow(&signers, 0);

        aggregator::new_test(acc1, 100, 0, false);
        let (val, dec, is_neg) = math::unpack(aggregator::latest_value(signer::address_of(acc1)));
        assert!(val == 100 * math128::pow(10, (dec as u128)), 0);
        assert!(dec == 9, 0);
        assert!(is_neg == false, 0);
    }
}
